# Parsers

## Strings

char = "\u000000"..

ascii = "\u000000".."\u00007F"

alpha = "a".."z" | "A".."Z"

alphas = many(alpha)

lower = "a".."z"

lowers = many(lower)

upper = "A".."Z"

uppers = many(upper)

numeral = "0".."9"

numerals = many(numeral)

binary_numeral = "0" | "1"

octal_numeral = "0".."7"

hex_numeral = numeral | "a".."f" | "A".."F"

alnum = alpha | numeral

alnums = many(alnum)

token = many(unless(char, whitespace))

word = many(alnum | "_" | "-")

line = chars_until(newline | end_of_input)

space =
  " " | "\t" | "\u0000A0" | "\u002000".."\u00200A" | "\u00202F" | "\u00205F" | "\u003000"

spaces = many(space)

newline = "\r\n" | "\u00000A".."\u00000D" | "\u000085" | "\u002028" | "\u002029"

nl = newline

newlines = many(newline)

nls = newlines

whitespace = many(space | newline)

ws = whitespace

chars_until(stop) = many_until(char, stop)

## Numbers

digit = 0..9

integer = number_of(maybe("-") + _number_integer_part)

int = integer

non_negative_integer = number_of(_number_integer_part)

negative_integer = number_of("-" + _number_integer_part)

float = number_of(maybe("-") + _number_integer_part + _number_fraction_part)

scientific_integer = number_of(
  maybe("-") +
  _number_integer_part +
  _number_exponent_part
)

scientific_float = number_of(
  maybe("-") +
  _number_integer_part +
  _number_fraction_part +
  _number_exponent_part
)

number = number_of(
  maybe("-") +
  _number_integer_part +
  maybe(_number_fraction_part) +
  maybe(_number_exponent_part)
)

num = number

non_negative_number = number_of(
  _number_integer_part +
  maybe(_number_fraction_part) +
  maybe(_number_exponent_part)
)

negative_number = number_of(
  "-" +
  _number_integer_part +
  maybe(_number_fraction_part) +
  maybe(_number_exponent_part)
)

_number_integer_part = ("1".."9" + numerals) | numeral

_number_fraction_part = "." + numerals

_number_exponent_part = ("e" | "E") + maybe("-" | "+") + numerals

binary_digit = 0..1

octal_digit = 0..7

hex_digit =
  digit |
  ("a" | "A" $ 10) |
  ("b" | "B" $ 11) |
  ("c" | "C" $ 12) |
  ("d" | "D" $ 13) |
  ("e" | "E" $ 14) |
  ("f" | "F" $ 15)

binary_integer = array(binary_digit) -> Digits $ Num.FromBinaryDigits(Digits)

octal_integer = array(octal_digit) -> Digits $ Num.FromOctalDigits(Digits)

hex_integer = array(hex_digit) -> Digits $ Num.FromHexDigits(Digits)

## Constants

true(t) = t $ true

false(f) = f $ false

boolean(t, f) = true(t) | false(f)

bool = boolean

null(n) = n $ null

## Array

array(elem) = elem -> First & _array(elem, [First])

_array(elem, Acc) =
  elem -> Elem ?
  _array(elem, [...Acc, Elem]) :
  const(Acc)

array_sep(elem, sep) = elem -> First & _array(sep > elem, [First])

array_until(elem, stop) =
  unless(elem, stop) -> First & _array_until(elem, stop, [First])

_array_until(elem, stop, Acc) =
  peek(stop) ?
  const(Acc) :
  elem -> Elem & _array_until(elem, stop, [...Acc, Elem])

maybe_array(elem) = default(array(elem), [])

maybe_array_sep(elem, sep) = default(array_sep(elem, sep), [])

tuple1(elem) =  elem -> Elem $ [Elem]

tuple2(elem1, elem2) = elem1 -> E1 & elem2 -> E2 $ [E1, E2]

tuple2_sep(elem1, sep, elem2) = elem1 -> E1 & sep & elem2 -> E2 $ [E1, E2]

tuple3(elem1, elem2, elem3) =
  elem1 -> E1 &
  elem2 -> E2 &
  elem3 -> E3 $
  [E1, E2, E3]

tuple3_sep(elem1, sep1, elem2, sep2, elem3) =
  elem1 -> E1 & sep1 &
  elem2 -> E2 & sep2 &
  elem3 -> E3 $
  [E1, E2, E3]

tuple(elem, N) =
  const(_Assert.NonNegativeInteger(N)) &
  _tuple(elem, N, [])

_tuple(elem, N, Acc) =
  const(N -> ..0) ?
  const(Acc) :
  elem -> Elem & _tuple(elem, Num.Dec(N), [...Acc, Elem])

tuple_sep(elem, sep, N) =
  const(_Assert.NonNegativeInteger(N)) &
  _tuple_sep(elem, sep, N, [])

_tuple_sep(elem, sep, N, Acc) =
  const(N -> ..0) ?
  const(Acc) :
  sep > elem -> Elem & _tuple_sep(elem, sep, Num.Dec(N), [...Acc, Elem])

table_sep(elem, sep, row_sep) =
  elem -> First & _table_sep(elem, sep, row_sep, [First], [])

_table_sep(elem, sep, row_sep, AccRow, AccRows) =
  sep > elem -> Elem ?
  _table_sep(elem, sep, row_sep, [...AccRow, Elem], AccRows) :
  row_sep > elem -> NextRow ?
  _table_sep(elem, sep, row_sep, [NextRow], [...AccRows, AccRow]) :
  const([...AccRows, AccRow])

maybe_table_sep(elem, sep, row_sep) =
  default(table_sep(elem, sep, row_sep), [[]])

## Objects

object(key, value) =
  key -> K & value -> V &
  _object(key, value, {K: V})

_object(key, value, Acc) =
  key -> K & value -> V ?
  _object(key, value, {...Acc, K: V}) :
  const(Acc)

object_sep(key, pair_sep, value, sep) =
  key -> K & pair_sep & value -> V &
  _object(sep > key, pair_sep > value, {K: V})

object_until(key, value, stop) =
  unless(key, stop) -> K &
  value -> V &
  _object_until(key, value, stop, {K: V})

_object_until(key, value, stop, Acc) =
  peek(stop) ?
  const(Acc) :
  key -> K & value -> V & _object_until(key, value, stop, {...Acc, K: V})

maybe_object(key, value) = default(object(key, value), {})

maybe_object_sep(key, pair_sep, value, sep) =
  default(object_sep(key, pair_sep, value, sep), {})

pair(key, value) = key -> K & value -> V $ {K: V}

pair_sep(key, sep, value) = key -> K & sep & value -> V $ {K: V}

record1(Key, value) = value -> Value $ {Key: Value}

record2(Key1, value1, Key2, value2) =
  value1 -> V1 &
  value2 -> V2 $
  {Key1: V1, Key2: V2}

record2_sep(Key1, value1, sep, Key2, value2) =
  value1 -> V1 & sep &
  value2 -> V2 $
  {Key1: V1, Key2: V2}

record3(Key1, value1, Key2, value2, Key3, value3) =
  value1 -> V1 &
  value2 -> V2 &
  value3 -> V3 $
  {Key1: V1, Key2: V2, Key3: V3}

record3_sep(Key1, value1, sep1, Key2, value2, sep2, Key3, value3) =
  value1 -> V1 & sep1 &
  value2 -> V2 & sep2 &
  value3 -> V3 $
  {Key1: V1, Key2: V2, Key3: V3}

## Repeated

many(p) = p -> First & _many(p, First)

_many(p, Acc) = p -> Next ? _many(p, Acc + Next) : const(Acc)

many_sep(p, sep) = p -> First & _many(sep > p, First)

many_until(p, stop) = unless(p, stop) -> First & _many_until(p, stop, First)

_many_until(p, stop, Acc) =
  peek(stop) ?
  const(Acc) :
  p -> Next & _many_until(p, stop, Acc + Next)

maybe_many(p) = many(p) | succeed

maybe_many_sep(p, sep) = many_sep(p, sep) | succeed

repeat2(p) = p + p

repeat3(p) = p + p + p

repeat4(p) = p + p + p + p

repeat5(p) = p + p + p + p + p

repeat6(p) = p + p + p + p + p + p

repeat7(p) = p + p + p + p + p + p + p

repeat8(p) = p + p + p + p + p + p + p + p

repeat9(p) = p + p + p + p + p + p + p + p + p

repeat(p, N) =
  const(_Assert.NonNegativeInteger(N)) &
  _repeat(p, N, $null)

_repeat(p, N, Acc) =
  const(N -> ..0) ?
  const(Acc) :
  p -> Next & _repeat(p, Num.Dec(N), Acc + Next)

repeat_between(p, N, M) =
  const(_Assert.NonNegativeInteger(N)) &
  const(_Assert.NonNegativeInteger(M)) &
  _repeat_between(p, N, M, $null)

_repeat_between(p, N, M, Acc) =
  const(M -> ..0) ?
  const(Acc) :
  p -> Next ?
  _repeat_between(p, Num.Dec(N), Num.Dec(M), Acc + Next) :
  const(N -> ..0) ?
  const(Acc) :
  @fail

one_or_both(a, b) = (a + maybe(b)) | (maybe(a) + b)

## Utility

peek(p) = p -> V ! const(V)

maybe(p) = p | succeed

unless(p, excluded) = excluded ? @fail : p

skip(p) = null(p)

find(p) = p | (char > find(p))

find_all(p) = array(find(p)) < maybe(many(char))

find_before(p, stop) = stop ? @fail :  p | (char > find_before(p, stop))

find_all_before(p, stop) = array(find_before(p, stop)) < maybe(chars_until(stop))

succeed = const($null)

default(p, D) = p | const(D)

const(C) = "" $ C

number_of = @number_of

string_of(p) = "%(p)"

surround(p, fill) = fill > p < fill

end_of_input = char ? @fail : succeed

end = end_of_input

input(p) = surround(p, maybe(whitespace)) < end_of_input

## JSON

json =
  json.boolean |
  json.null |
  json.number |
  json.string |
  json.array(json) |
  json.object(json)

json.boolean = boolean("true", "false")

json.null = null("null")

json.number = number

json.string = '"' > _json.string_body < '"'

_json.string_body =
  many(
    _escaped_ctrl_char |
    _escaped_unicode |
    unless(char, _ctrl_char | `\` | '"')
  ) | const($"")

_ctrl_char = "\u000000".."\u00001F"

_escaped_ctrl_char =
  (`\"` $ `"`) |
  (`\\` $ `\`) |
  (`\/` $ `/`) |
  (`\b` $ "\b") |
  (`\f` $ "\f") |
  (`\n` $ "\n") |
  (`\r` $ "\r") |
  (`\t` $ "\t")

_escaped_unicode = _escaped_surrogate_pair | _escaped_codepoint

_escaped_surrogate_pair = _valid_surrogate_pair | _invalid_surrogate_pair

_valid_surrogate_pair =
  _high_surrogate -> H & _low_surrogate -> L $ @SurrogatePairCodepoint(H, L)

_invalid_surrogate_pair = _low_surrogate | _high_surrogate $ "\u00FFFD"

_high_surrogate =
  `\u` > ("D" | "d") + ("8" | "9" | "A" | "B" | "a" | "b") + hex_numeral + hex_numeral

_low_surrogate =
  `\u` > ("D" | "d") + ("C".."F" | "c".."f") + hex_numeral + hex_numeral

_escaped_codepoint = `\u` > repeat4(hex_numeral) -> U $ @Codepoint(U)

json.array(elem) = "[" > maybe_array_sep(surround(elem, maybe(ws)), ",") < "]"

json.object(value) =
  "{" >
  maybe_object_sep(
    surround(json.string, maybe(ws)), ":",
    surround(value, maybe(ws)), ","
  )
  < "}"

## TOML

toml = toml.simple

toml.simple = toml.custom(toml.simple_value)

toml.tagged = toml.custom(toml.tagged_value)

toml.custom(value) =
  default(toml.root_table(value, {}), {}) -> RootDoc &
  toml.tables(value, RootDoc) -> FinalDoc &
  maybe(maybe(ws) > _toml.comment) $
  FinalDoc

toml.root_table(value, Doc) = _toml.table_body(value, [], Doc)

toml.tables(value, Doc) =
  _toml.ws &
  (toml.table(value, Doc) | toml.array_of_tables(value, Doc)) -> NewDoc ?
  toml.tables(value, NewDoc) :
  const(Doc)

toml.table(value, Doc) =
  _toml.table_header -> HeaderPath &
  const(_Toml.InsertAtPath(Doc, HeaderPath, {})) -> NewDoc &
  maybe(spaces) & maybe(_toml.comment) & maybe(spaces) &
  (nl ? _toml.table_body(value, HeaderPath, NewDoc) : const(NewDoc))

toml.array_of_tables(value, Doc) =
  _toml.array_of_tables_header -> HeaderPath &
  const(_Toml.AppendAtPath(Doc, HeaderPath, {})) -> NewDoc &
  maybe(spaces) & maybe(_toml.comment) & maybe(spaces) &
  (nl ? _toml.table_body(value, HeaderPath, NewDoc) : const(NewDoc))

_toml.ws = maybe_many(ws | _toml.comment)

_toml.table_header = "[" > maybe(ws) > _toml.path < maybe(ws) < "]"

_toml.array_of_tables_header =
  repeat2("[" > maybe(ws)) > _toml.path < repeat2(maybe(ws) < "]")

_toml.table_body(value, HeaderPath, Doc) =
  _toml.ws &
  _toml.table_pair(value, HeaderPath, Doc) -> NewDoc ?
  _toml.table_body(value, HeaderPath, NewDoc) :
  const(Doc)

_toml.table_pair(value, HeaderPath, Doc) =
  _toml.path -> KeyPath &
  maybe(spaces) & "=" & maybe(spaces) &
  value -> Val &
  maybe(spaces) & maybe(_toml.comment) & nl | end & maybe(ws) $
  _Toml.InsertAtPath(Doc, HeaderPath + KeyPath, Val)

_toml.path = array_sep(_toml.key, surround(".", maybe(ws)))

_toml.key =
  many(alpha | numeral | "_" | "-") |
  toml.string.basic |
  toml.string.literal

_toml.comment = "#" > maybe(line)

toml.simple_value =
  toml.string |
  toml.datetime |
  toml.number |
  toml.boolean |
  toml.array(toml.simple_value) |
  toml.inline_table(toml.simple_value)

toml.tagged_value =
  toml.string |
  _toml.tag($"datetime", $"offset", toml.datetime.offset) |
  _toml.tag($"datetime", $"local", toml.datetime.local) |
  _toml.tag($"datetime", $"date-local", toml.datetime.local_date) |
  _toml.tag($"datetime", $"time-local", toml.datetime.local_time) |
  _toml.tag($"integer", $"binary", toml.number.binary_numeral) |
  _toml.tag($"integer", $"octal", toml.number.octal_numeral) |
  _toml.tag($"integer", $"hexadecimal", toml.number.hex_numeral) |
  _toml.tag($"float", $"infinity", toml.number.infinity) |
  _toml.tag($"float", $"not-a-number", toml.number.not_a_number) |
  toml.number.float |
  toml.number.integer |
  toml.boolean |
  toml.array(toml.tagged_value) |
  toml.inline_table(toml.tagged_value)

_toml.tag(Type, Subtype, value) =
  value -> Value $ {"type": Type, "subtype": Subtype, "value": Value}

toml.string =
  toml.string.multi_line_basic |
  toml.string.multi_line_literal |
  toml.string.basic |
  toml.string.literal

toml.datetime =
  toml.datetime.offset |
  toml.datetime.local |
  toml.datetime.local_date |
  toml.datetime.local_time

toml.number =
  toml.number.binary_numeral |
  toml.number.octal_numeral |
  toml.number.hex_numeral |
  toml.number.infinity |
  toml.number.not_a_number |
  toml.number.float |
  toml.number.integer

toml.boolean = boolean("true", "false")

toml.array(elem) =
  "[" > _toml.ws > default(
    array_sep(surround(elem, _toml.ws), ",") < maybe(surround(",", _toml.ws)),
    []
  ) < _toml.ws < "]"

toml.inline_table(value) = "{" > _toml.inline_table_body(value, {}) < "}"

_toml.inline_table_body(value, Table) =
  _toml.inline_table_pair(value, Table) -> NewTable ?
  ("," ? _toml.inline_table_body(value, NewTable) : const(NewTable)) :
  const(Table)

_toml.inline_table_pair(value, Doc) =
  maybe(spaces) &
  _toml.path -> Key &
  maybe(spaces) & "=" & maybe(spaces) &
  value -> Val &
  maybe(spaces) $
  _Toml.InsertAtPath(Doc, Key, Val)

toml.string.multi_line_basic = `"""` > maybe(ws) > _toml.string.multi_line_basic_body < `"""`

_toml.string.multi_line_basic_body =
  many(
    _toml.escaped_ctrl_char |
    _toml.escaped_unicode |
    ws |
    skip(`\` + ws) |
    (peek(`"""""`) > `""`) |
    (peek(`""""`) > `"`) |
    unless(char, _ctrl_char | `\` | `"""`)
  ) | const($"")

toml.string.multi_line_literal = `'''` > maybe(ws) > _toml.string.multi_line_literal_body < `'''`

_toml.string.multi_line_literal_body =
  many(
    (peek(`'''''`) > `''`) |
    (peek(`''''`) > `'`) |
    unless(char, `'''`)
  ) | const($"")

toml.string.basic = '"' > _toml.string.basic_body < '"'

_toml.string.basic_body =
  many(
    _toml.escaped_ctrl_char |
    _toml.escaped_unicode |
    unless(char, _ctrl_char | `\` | '"')
  ) | const($"")

toml.string.literal = "'" > default(chars_until("'"), $"") < "'"

_toml.escaped_ctrl_char =
  (`\b` $ "\b") |
  (`\t` $ "\t") |
  (`\n` $ "\n") |
  (`\f` $ "\f") |
  (`\r` $ "\r") |
  (`\"` $ `"`) |
  (`\\` $ `\`)

_toml.escaped_unicode =
  (`\u` > repeat4(hex_numeral) -> U $ @Codepoint(U)) |
  (`\U` > repeat8(hex_numeral) -> U $ @Codepoint(U))

toml.datetime.offset = toml.datetime.local_date + ("T" | "t" | " ") + _toml.datetime.time_offset

toml.datetime.local = toml.datetime.local_date + ("T" | "t" | " ") + toml.datetime.local_time

toml.datetime.local_date =
  repeat4(numeral) + "-" + repeat2(numeral) + "-" + repeat2(numeral)

toml.datetime.local_time =
  repeat2(numeral) + ":" + repeat2(numeral) + ":" + repeat2(numeral) +
  maybe("." + repeat_between(numeral, $1, $9))

_toml.datetime.time_offset = toml.datetime.local_time + ("Z" | "z" | _toml.datetime.time_numoffset)

_toml.datetime.time_numoffset = ("+" | "-") + repeat2(numeral) + ":" + repeat2(numeral)

toml.number.integer = number_of(
  _toml.number.sign +
  _toml.number.integer_part
)

_toml.number.sign = maybe("-" | skip("+"))

_toml.number.integer_part =
  ("1".."9" + many(maybe("_") > numeral)) | numeral

toml.number.float = number_of(
  _toml.number.sign +
  _toml.number.integer_part + (
    (_toml.number.fraction_part + maybe(_toml.number.exponent_part)) |
    _toml.number.exponent_part
  )
)

_toml.number.fraction_part = "." + numeral + maybe_many(maybe("_") > numeral)

_toml.number.exponent_part = ("e" | "E") + maybe("-" | "+") + numerals

toml.number.infinity = maybe("+" | "-") + "inf"

toml.number.not_a_number = maybe("+" | "-") + "nan"

toml.number.binary_numeral =
  "0b" + one_or_both(
    many_sep("0", maybe("_")) + maybe(skip("_") < peek(binary_numeral)),
    many_sep(binary_numeral, maybe("_"))
  )

toml.number.octal_numeral =
  "0o" + one_or_both(
    many_sep("0", maybe("_")) + maybe(skip("_") < peek(octal_numeral)),
    many_sep(octal_numeral, maybe("_"))
  )

toml.number.hex_numeral =
  "0x" + one_or_both(
    many_sep("0", maybe("_")) + maybe(skip("_") < peek(hex_numeral)),
    many_sep(hex_numeral, maybe("_"))
  )

toml.number.binary_integer =
  "0b" & one_or_both(
    array_sep(0, maybe("_")) + maybe(skip("_") < peek(binary_integer)),
    array_sep(binary_integer, maybe("_"))
  ) -> Digits $
  Num.FromBinaryDigits(Digits)

toml.number.octal_integer =
  "0o" & one_or_both(
    array_sep(0, maybe("_")) + maybe(skip("_") < peek(octal_integer)),
    array_sep(octal_integer, maybe("_"))
  ) -> Digits $
  Num.FromOctalDigits(Digits)

toml.number.hex_integer =
  "0x" & one_or_both(
    array_sep(0, maybe("_")) + maybe(skip("_") < peek(hex_integer)),
    array_sep(hex_integer, maybe("_"))
  ) -> Digits $
  Num.FromHexDigits(Digits)

_Toml.InsertAtPath(Obj, Path, Val) =
  Path -> [Key] ? (
    Obj -> {Key: _, ..._} ?
    (Val -> {} ? Obj : @Fail) :
    {...Obj, Key: Val}
  ) :
  Path -> [Key, ...PathRest] ? (
    Obj -> {Key: ExistingVal, ..._} ? (
      ExistingVal -> [...ArrayOfTables, {...NestedObj}] ?
      {...Obj, Key: [...ArrayOfTables, _Toml.InsertAtPath(NestedObj, PathRest, Val)]} :
      ExistingVal -> {...NestedObj} &
      {...Obj, Key: _Toml.InsertAtPath(NestedObj, PathRest, Val)}
    ) :
    {...Obj, Key: _Toml.InsertAtPath({}, PathRest, Val)}
  ) :
  Obj

_Toml.AppendAtPath(Obj, Path, Val) =
  Path -> [Key] ? (
    Obj -> {Key: ExistingVal, ..._} ?
    Is.Array(ExistingVal) & {...Obj, Key: [...ExistingVal, Val]} :
    {...Obj, Key: [Val]}
  ) :
  Path -> [Key, ...PathRest] ? (
    Obj -> {Key: ExistingVal, ..._} ? (
      ExistingVal -> [...ArrayOfTables, {...NestedObj}] ?
      {...Obj, Key: [...ArrayOfTables, _Toml.AppendAtPath(NestedObj, PathRest, Val)]} :
      ExistingVal -> {...NestedObj} &
      {...Obj, Key: _Toml.AppendAtPath(NestedObj, PathRest, Val)}
    ) :
    {...Obj, Key: _Toml.AppendAtPath({}, PathRest, Val)}
  ) :
  Obj

## Abstract Syntax Trees

ast.with_operator_precedence(operand, prefix, infix, postfix) =
  _ast.with_precedence_start(operand, prefix, infix, postfix, $0)

_ast.with_precedence_start(operand, prefix, infix, postfix, LeftBindingPower) =
  prefix -> [OpNode, PrefixBindingPower] ? (
    _ast.with_precedence_start(
      operand, prefix, infix, postfix,
      PrefixBindingPower
    ) -> PrefixedNode &
    _ast.with_precedence_rest(
      operand, prefix, infix, postfix,
      LeftBindingPower,
      {...OpNode, "prefixed": PrefixedNode}
    )
  ) : (
    operand -> Node &
    _ast.with_precedence_rest(operand, prefix, infix, postfix, LeftBindingPower, Node)
  )

_ast.with_precedence_rest(operand, prefix, infix, postfix, LeftBindingPower, Node) =
  postfix -> [OpNode, RightBindingPower] &
  const(Is.LessThan(LeftBindingPower, RightBindingPower)) ? (
    _ast.with_precedence_rest(
      operand, prefix, infix, postfix,
      LeftBindingPower,
      {...OpNode, "postfixed": Node}
    )
  ) :
  infix -> [OpNode, RightBindingPower, NextLeftBindingPower] &
  const(Is.LessThan(LeftBindingPower, RightBindingPower)) ? (
    _ast.with_precedence_start(
      operand, prefix, infix, postfix,
      NextLeftBindingPower
    ) -> RightNode &
    _ast.with_precedence_rest(
      operand, prefix, infix, postfix,
      LeftBindingPower,
      {...OpNode, "left": Node, "right": RightNode}
    )
  ) :
  const(Node)

ast.node(Type, value) =
  value -> Value $ {"type": Type, "value": Value}

# Values

## Numbers

Num.Add = @Add

Num.Sub = @Subtract

Num.Mul = @Multiply

Num.Div = @Divide

Num.Pow = @Power

Num.Inc(N) = @Add(N, 1)

Num.Dec(N) = @Subtract(N, 1)

Num.Abs(N) = N -> 0.. | -N

Num.FromBinaryDigits(Bs) =
  Array.Length(Bs) -> Len &
  _Num.FromBinaryDigits(Bs, Len - 1, 0)

_Num.FromBinaryDigits(Bs, Pos, Acc) =
  Bs -> [B, ...Rest] ? (
    B -> 0..1 &
    _Num.FromBinaryDigits(
      Rest,
      Pos - 1,
      Acc + Num.Mul(B, Num.Pow(2, Pos)),
    )
  ) :
  Acc

Num.FromOctalDigits(Os) =
  Array.Length(Os) -> Len &
  _Num.FromOctalDigits(Os, Len - 1, 0)

_Num.FromOctalDigits(Os, Pos, Acc) =
  Os -> [O, ...Rest] ? (
    O -> 0..7 &
    _Num.FromOctalDigits(
      Rest,
      Pos - 1,
      Acc + Num.Mul(O, Num.Pow(8, Pos)),
    )
  ) :
  Acc

Num.FromHexDigits(Hs) =
  Array.Length(Hs) -> Len &
  _Num.FromHexDigits(Hs, Len - 1, 0)

_Num.FromHexDigits(Hs, Pos, Acc) =
  Hs -> [H, ...Rest] ? (
    H -> 0..15 &
    _Num.FromHexDigits(
      Rest,
      Pos - 1,
      Acc + Num.Mul(H, Num.Pow(16, Pos)),
    )
  ) :
  Acc

## Arrays

Array.First(Array) = Array -> [F, ..._] & F

Array.Rest(Array) = Array -> [_, ...R] & R

Array.Length(A) = _Array.Length(A, 0)

_Array.Length(A, Acc) =
  A -> [_, ...Rest] ?
  _Array.Length(Rest, Acc + 1) :
  Acc

Array.Reverse(A) = _Array.Reverse(A, [])

_Array.Reverse(A, Acc) =
  A -> [First, ...Rest] ?
  _Array.Reverse(Rest, [First, ...Acc]) :
  Acc

Array.Map(A, Fn) = _Array.Map(A, Fn, [])

_Array.Map(A, Fn, Acc) =
  A -> [First, ...Rest] ?
  _Array.Map(Rest, Fn, [...Acc, Fn(First)]) :
  Acc

Array.Filter(A, Pred) = _Array.Filter(A, Pred, [])

_Array.Filter(A, Pred, Acc) =
  A -> [First, ...Rest] ?
  _Array.Filter(Rest, Pred, Pred(First) ? [...Acc, First] : Acc) :
  Acc

Array.Reject(A, Pred) = _Array.Reject(A, Pred, [])

_Array.Reject(A, Pred, Acc) =
  A -> [First, ...Rest] ?
  _Array.Reject(Rest, Pred, Pred(First) ? Acc : [...Acc, First]) :
  Acc

Array.ZipObject(Ks, Vs) = _Array.ZipObject(Ks, Vs, {})

_Array.ZipObject(Ks, Vs, Acc) =
  Ks -> [K, ...KsRest] & Vs -> [V, ...VsRest] ?
  _Array.ZipObject(KsRest, VsRest, {...Acc, K: V}) :
  Acc

Array.ZipPairs(A1, A2) = _Array.ZipPairs(A1, A2, [])

_Array.ZipPairs(A1, A2, Acc) =
  A1 -> [First1, ...Rest1] & A2 -> [First2, ...Rest2] ?
  _Array.ZipPairs(Rest1, Rest2, [...Acc, [First1, First2]]) :
  Acc

Table.Transpose(T) = _Table.Transpose(T, [])

_Table.Transpose(T, Acc) =
  Array.Map(T, Array.First) -> FirstPerRow &
  Array.Map(T, Array.Rest) -> RestPerRow ?
  _Table.Transpose(RestPerRow, [...Acc, FirstPerRow]) :
  Acc

Table.RotateClockwise(T) = Array.Map(Table.Transpose(T), Array.Reverse)

Table.RotateCounterClockwise(T) = Array.Reverse(Table.Transpose(T))

Table.ZipObjects(Ks, Rows) = _Table.ZipObjects(Ks, Rows, [])

_Table.ZipObjects(Ks, Rows, Acc) =
  Rows -> [Row, ...Rest] ?
  _Table.ZipObjects(Ks, Rest, [...Acc, Array.ZipObject(Ks, Row)]) :
  Acc

## Objects

Obj.Get(O, K) = O -> {K: V, ..._} & V

Obj.Put(O, K, V) = {...O, K: V}

## Abstract Syntax Trees

Ast.Precedence(OpNode, BindingPower) = [OpNode, BindingPower]

Ast.InfixPrecedence(OpNode, LeftBindingPower, RightBindingPower) =
  [OpNode, LeftBindingPower, RightBindingPower]

## Predicates

Is.String(V) = V -> ("" + _)

Is.Number(V) = V -> (0 + _)

Is.Bool(V) = V -> (false + _)

Is.Null(V) = V -> null

Is.Array(V) = V -> [..._]

Is.Object(V) = V -> {..._}

Is.Equal(A, B) = A -> B

Is.LessThan(A, B) = A -> B ? @Fail : A -> ..B

Is.LessThanOrEqual(A, B) = A -> ..B

Is.GreaterThan(A, B) = A -> B ? @Fail : A -> B..

Is.GreaterThanOrEqual(A, B) = A -> B..

## Utility

_Assert.NonNegativeInteger(V) =
  V -> 0.. | @Crash("Expected a non-negative integer, got %(V)")
