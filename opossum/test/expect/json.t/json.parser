json ;

json =
  json_string |
  json_true   |
  json_false  |
  json_null   |
  json_number |
  json_array  |
  json_object ;

json_string =
  '"' > string_content < '"' ;

string_content =
  (["\\", C] <- two_chars & Str <- const(C) + string_content $ Str) |
  (['"', _Outside] <- peek(two_chars) $ "") |
  (C <- char & Str <- const(C) + string_content $ Str) ;

json_true = true("true") ;

json_false = false("false") ;

json_null = null("null") ;

json_number = number ;

json_array =
  "[" & mws &
  A <- maybe_array_sep(mws > json < mws, ",") &
  mws & "]" $
  A ;

json_object =
  "{" & mws &
  O <- maybe_object_sep(mws > json_string < mws, ":", mws > json < mws, ",")
  & mws & "}" $
  O ;

###
# Helpers
###

mws = maybe(whitespace) ;

two_chars = A <- maybe(char) & B <- maybe(char) $ [A, B] ;
