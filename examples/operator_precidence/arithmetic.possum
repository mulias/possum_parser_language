w = maybe(whitespace)

var_node = ast.node(alphas, $"var")

number_node = ast.node(non_negative_number, $"num")

grouped_expr_node = "(" > w > expr < w < ")"

operand = var_node | number_node | grouped_expr_node

conditional(Type, LeftBindingPower, RightBindingPower) =
  "?" & w & expr -> Middle & w & ":" $
  {"type": Type, "middle": Middle, "power": [LeftBindingPower, RightBindingPower]}

index_access(Type, BindingPower) =
  "[" > w > expr < w < "]" -> Index $
  {"type": Type, "index": Index, "power": BindingPower}

prefix =
  ast.prefix_node("-", $"neg", $7)

infix =
  ast.infix_node("^", $"exp", $6, $6.5) |
  ast.infix_node("*", $"mul", $5, $5.5) |
  ast.infix_node("/", $"div", $5, $5.5) |
  ast.infix_node("+", $"add", $4, $4.5) |
  ast.infix_node("-", $"sub", $4, $4.5) |
  ast.infix_node("==", $"eql", $3, $3.5) |
  conditional($"cond", $2.5, $2) |
  ast.infix_node("=", $"assign", $1.5, $1)

postfix =
  ast.postfix_node("!", $"fac", $8) |
  index_access($"index", $8)

node(p) = surround(ast.with_offset_pos(p), w)

expr = ast.with_operator_precedence(
  node(operand),
  node(prefix),
  node(infix),
  node(postfix)
)

expr
