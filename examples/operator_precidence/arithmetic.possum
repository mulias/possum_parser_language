w = maybe(whitespace)

var_node = ast_node($"var", alphas)

number_node = ast_node($"num", non_negative_number)

grouped_expr = "(" > w > expr < w < ")"

operand = var_node | number_node | grouped_expr

conditional_infix =
  "?" & w & expr -> Middle & w & ":" $ {"type": "cond", "middle": Middle}

index_postfix = ast_node($"index", "[" > w > expr < w < "]")

prefix =
  ("-" $ AstOpPrecedence({"type": "neg"}, 7))

infix =
  ("^"  $ AstInfixOpPrecedence({"type": "exp"},    6, 6.5)) |
  ("*"  $ AstInfixOpPrecedence({"type": "mul"},    5, 5.5)) |
  ("/"  $ AstInfixOpPrecedence({"type": "div"},    5, 5.5)) |
  ("+"  $ AstInfixOpPrecedence({"type": "add"},    4, 4.5)) |
  ("-"  $ AstInfixOpPrecedence({"type": "sub"},    4, 4.5)) |
  ("==" $ AstInfixOpPrecedence({"type": "eql"},    3, 3.5)) |
  (conditional_infix ->
   Node $ AstInfixOpPrecedence(Node,               2.5, 2)) |
  ("="  $ AstInfixOpPrecedence({"type": "assign"}, 1.5, 1))

postfix =
  ("!" $ AstOpPrecedence({"type": "fac"}, 8)) |
  (index_postfix -> Node $ AstOpPrecedence(Node, 8))

expr = ast_with_operator_precedence(
  surround(operand, w),
  surround(prefix, w),
  surround(infix, w),
  surround(postfix, w)
)

expr
